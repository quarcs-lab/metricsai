* ch03_The_Sample_Mean.do - January 2026 For Stata
* Updated for online execution portability

********** OVERVIEW OF ch03_The_Sample_Mean.do **********

* STATA Program
* copyright C 2021 by A. Colin Cameron
* Used for "Analyis of Economics Data: An Introduction to Econometrics"
* by A. Colin Cameron (2021)

* To run you need file
*   AED_COINTOSSMEANS.DTA
*   AED_CENSUSAGEMEANS.DTA

* in your directory

********** SETUP **********

* Set random seed for reproducibility
set seed 42

* Clear all macros
macro drop _all

* GitHub data URL
global github_data_url "https://raw.githubusercontent.com/quarcs-lab/data-open/master/AED/"

* Create output directories (optional - for saving figures and tables locally)
cap mkdir "images"
cap mkdir "tables"

set more off
clear all

************

* This STATA does analysis for Chapter 3
*   3.1 RANDOM VARIABLES
*   3.2 SAMPLE GENERATED BY AN EXPERIMENT: COIN TOSSES
*   3.3 PROPERTIES OF THE SAMPLE MEAM
*   3.4 SAMPLING FROM A FINITE POPULATION: 1880 U.S. CENSUS
*   3.5 ESTIMATION OF THE POPULATION MEAN
*   3.6 SAMPLES OTHER THAN RANDOM SAMPLES
*   3.7 COMPUTER GENERATION OF A RANDOM SAMPLE

********** DATA DESCRIPTION

* (1) Coin toss means data is in AED_COINTOSSMEANS.DTA
*     The code to generate this is given at the end of this program
* (2) 1880 Census data in AED_CENSUSAGEMEANS.DTA was drived as follows
*     First the entire Census was obtained from IPUMS
*     Second Stata code was written to randomly select subsamples without replacement
*     See makedata_CENSUSAGEMEANS.do

****   3.2 SAMPLE GENERATED BY AN EXPERIMENT: COIN TOSSES

* Draw one sample of size 30 from Bernoulli mu = p = 0.5 
clear
set obs 30
set seed 10101
generate u = runiform()
generate x = u > 0.5
summarize x
label variable x "Heads = 1 and Tails = 0"

* Figure 3.1: First panel
histogram x
graph export "images/ch03_fig1a.png", replace

* Means from 400 samples of 30 coin tosses for a fair coin
* See the code at the end of this file that creates this dataset

* Download and load data from GitHub for 400 coin tosses
clear
copy "${github_data_url}AED_COINTOSSMEANS.DTA" "temp_data.dta", replace
use "temp_data.dta", clear
erase "temp_data.dta"
list in 1/5
describe
summarize
summarize xbar

* Default histogram has empty bins as xbar can only take values
*   0, 1/30, 2/30, ...., 29/30, 1
histogram xbar, normal

* Figure 3.1: Second panel
* only 12 bins
histogram xbar, normal bin(12)
graph export "images/ch03_fig1b.png", replace

****  3.4 SAMPLING FROM A FINITE POPULATION: 1880 U.S. CENSUS

* These data based on a very large dataset not given here.

* Figure 3.2  Histogram of the very large dataset
* Not given here

* Figure 3.3 first panel - again from the very large dataset
* Not given here

* Figure 3.3 second panel - uses the following data

* Download and load data from GitHub - means from 100 samples of size 25
clear
copy "${github_data_url}AED_CENSUSAGEMEANS.DTA" "temp_data.dta", replace
use "temp_data.dta", clear
erase "temp_data.dta"
describe
summarize
list in 1/5

* Figure 3.3 second panel  Histogram for 100 means
histogram mean, normal width(2)
graph export "images/ch03_fig3.png", replace

****   3.7 COMPUTER GENERATION OF A RANDOM SAMPLE

* Single sample
set obs 100
set seed 10101
generate x=runiform(3,9)
generate y=rnormal(5,2)
summarize x y

* Multiple samples for a simulation
* First define one sample of 30 coin tosses
program onesample, rclass
drop _all
set obs 30
generate u = runiform()
generate x = u > 0.5
summarize x
return scalar xbar = r(mean)
return scalar stdev = r(sd)
return scalar numobs = r(N)
end
* Then do 400 simulations
simulate xbar=r(xbar) stdev=r(stdev) numobs=r(numobs), seed(10101) reps(400): onesample
label variable xbar "Sample mean for each of 400 samples"
label variable stdev "Sample standard deviation for each of 400 samples"
label variable numobs "Number of observations in each sample"
describe 
summarize

********** CLOSE OUTPUT

display ""
display "Chapter 03 analysis complete."
display "Figures saved to: images/"
display "Tables saved to: tables/"
